Function: lfunartin
Section: l_functions
C-Name: lfunartin
Prototype: GGGG
Help: lfunartin(nf,gal,M,n): returns the Ldata structure associated to the
 Artin L-function associated to the representation R of the Galois group
 of the extension K/Q, defined over the cyclotomic field Q(zeta_n),
 where nf is the nfinit structure associated to K,
 gal is the galoisinit structure associated to K/Q, and M is the vector
 of the image of the generators G.gen by R. The elements of M are matrices
 with polynomial entries, whose variable is understood as the complex
 number exp(2*I*Pi/n).
Doc: returns the Ldata structure associated to the
 Artin $L$-function associated to the representation $\rho$ of the Galois group
 of the extension $K/\Q$, defined over the cyclotomic field $\Q(\zeta_n)$,
 where \kbd{nf} is the nfinit structure associated to $K$,
 \kbd{gal} is the galoisinit structure associated to $K/\Q$, and \kbd{M} is
 the vector of the image of the generators \kbd{G.gen} by $\rho$.
 The elements of \kbd{M} are matrices with polynomial entries, whose variable
 is understood as the complex number $\exp(2\*i\*\pi/n)$.

 In the following example we build the Artin $L$-functions associated to the two
 irreducible degree-$2$ representations of the dihedral group $D_{10}$ defined
 over $\Q(\zeta_5)$, for the
 extension $H/Q$ where $H$ is the Hilbert class field of $\Q(\sqrt{-47})$.
 We show numerically some identities involving Dedekind $\zeta$ functions and
 Hecke $L$ series.
 \bprog
 ? P=quadhilbert(-47);Q=nfsplitting(P);
 ? N=nfinit(Q);G=galoisinit(N);
 ? L1=lfunartin(N,G,[[a,0;0,a^-1],[0,1;1,0]],5);
 ? L2=lfunartin(N,G,[[a^2,0;0,a^-2],[0,1;1,0]],5);
 ? lfun(1,1)*lfun(-47,1)*lfun(L1,1)^2*lfun(L2,1)^2 - lfun(Q,1)
 %5 ~ 0
 ? lfun(1,1)*lfun(L1,1)*lfun(L2,1) - lfun(P,1)
 %6 ~ 0
 ? bnf=bnfinit(x^2+47);bnr=bnrinit(bnf,1,1);
 ? lfun([bnr,[1]],1) - lfun(L1,1)
 %7 ~ 0
 ? lfun([bnr,[2]],1) - lfun(L2,1)
 %8 ~ 0
 ? lfun(1,1)*lfun([bnr,[1]],1)*lfun([bnr,[2]],1) - lfun(P,1)
 %9 ~ 0
 @eprog
 The first identity is the factorisation of the regular representation of $D_{10}$,
 the second the factorisation of the natural representation of $D_{10}\subset S_5$,
 the next two are the expressions of the degree-$2$ representations as induced from
 degree-$1$ representations. The last one is a collorary of the others.
